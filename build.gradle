plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    repositories {
        maven { url 'https://jitpack.io' }
    }

    loom {
        silentMojangMappingsLicense()
    }

    configurations {
        shade
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

        implementation("com.github.LlamaLad7:MixinExtras:0.1.1")
        annotationProcessor("com.github.LlamaLad7:MixinExtras:0.1.1")
        annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

        shade "com.github.LlamaLad7:MixinExtras:0.1.1"
    }

    shadowJar {
        relocate 'com.github.LlamaLad7', 'com.immersive_armors'
    }
}

def getAppVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    def commitId = stdout.toString().replace("\n", "").replace("\r", "").trim()
    stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--points-at', commitId
        standardOutput = stdout
    }
    def tagName = stdout.toString().replace("\n", "").replace("\r", "").trim()
    def versionName = '0-git-' + commitId
    if (tagName != null && "" != tagName) {
        versionName = tagName
    }
    return versionName
} as Object

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = getAppVersion()

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 16
    }

    java {
        withSourcesJar()
    }
}
